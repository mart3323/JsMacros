plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

archivesBaseName = archivesBaseName + "-FORGE"

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    include
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

loom {
    forge {
        mixinConfigs "fabric-command-api-v1.mixins.json", "jsmacros-forge.mixins.json"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    implementation 'org.spongepowered:mixin:0.8.3'
    shadowCommon 'org.spongepowered:mixin:0.8.3'

    include "org.graalvm.sdk:graal-sdk:${project.graal_version}"
    include "org.graalvm.js:js:${project.graal_version}", {
        exclude module: "icu4j"
    }
    include "org.graalvm.truffle:truffle-api:${project.graal_version}"
    include "org.graalvm.regex:regex:${project.graal_version}"
//		include "org.graalvm.compiler:compiler:${project.graal_version}"
//		include "org.graalvm.compiler:compiler-management:${project.graal_version}"
    include "io.noties:prism4j:${project.prism_version}", {
        exclude module: "annotations-java5"
    }
    include "com.neovisionaries:nv-websocket-client:2.10"
    include "org.javassist:javassist:3.28.0-GA"
    include "com.mojang:brigadier:${project.brigadier_version}"


    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"

    // exclude icu that's "accidentally" (I hope) included in graaljs jar
    exclude "com/ibm/**"
    exclude "META-INF/**.SF"
    exclude "META-INF/**.RSA"
    exclude "META-INF/**.DSA"

    mergeServiceFiles()
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null

    from(configurations.include.files) {
        include '*'
        into 'META-INF/jars/'
    }
}

jar {
    classifier "dev"

    manifest {
        attributes([
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'FMLCorePlugin': 'xyz.wagyourtail.jsmacros.forge.client.JsMacrosEarlyRiser',
                'JsMacrosDeps': configurations.include.files.collect { it.name }.join(" "),
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true
        ])
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

task createDist(type: Copy) {
    from(new File(project.buildDir, "libs"))
    into new File(rootProject.rootDir, "dist")
}

build.finalizedBy(createDist)